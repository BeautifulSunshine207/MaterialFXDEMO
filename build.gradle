import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'java-library'
    id 'org.openjfx.javafxplugin' version "$jfxPlugin" apply false
    id 'org.beryx.jlink' version "$jlink" apply false
    id 'com.vanniktech.maven.publish' version "$mavenPublishPlugin" apply false
}

ext {
    scriptsDir = "$rootDir/scripts"
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "org.openjfx.javafxplugin"
    apply plugin: "com.vanniktech.maven.publish"

    // Remove vanniktech nonsense
    gradle.taskGraph.whenReady { graph ->
        if (graph.hasTask(plainJavadocJar)) {
            plainJavadocJar.enabled = false
        }
    }

    group 'io.github.palexdev'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        testImplementation platform("org.junit:junit-bom:$junit")
        testImplementation 'org.junit.jupiter:junit-jupiter'

        testImplementation "org.testfx:testfx-junit5:$testfx"
        testImplementation("io.github.palexdev:scenicview:$scenicView") {
            exclude group: "org.openjfx"
        }
    }

    javafx {
        // Workaround for Linux ARM64 because JavaFX is not updated as often for this architecture
        var isLinuxArm = (Os.isFamily(Os.FAMILY_UNIX)) && "aarch64".equals(System.getProperty("os.arch"))
        version = isLinuxArm ? "21.0.1" : "$jfx"
        modules = ["javafx.controls", "javafx.fxml"]
        configurations = ['implementation', 'testImplementation']
    }

    compileJava {
        sourceCompatibility = "$jdk"
        targetCompatibility = "$jdk"
    }

    compileTestJava {
        sourceCompatibility = "$testJdk"
        targetCompatibility = "$testJdk"
    }

    test {
        useJUnitPlatform()
    }
}



