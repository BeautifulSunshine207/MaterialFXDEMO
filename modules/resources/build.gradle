import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'io.miret.etienne.sass' version "$gradleSass"
}

// Cross-platform scripts
apply from: "$scriptsDir/updateJFXRes.gradle"
apply from: "$scriptsDir/processCSS.gradle"

version = "$mfxresources"

dependencies {
    api(project(":effects"))

    testImplementation "org.kordamp.ikonli:ikonli-fluentui-pack:$ikonli"
    testImplementation "org.kordamp.ikonli:ikonli-win10-pack:$ikonli"
}

java {
    withSourcesJar()
    withJavadocJar()
    tasks.withType(Jar).each { it.archiveBaseName.set("mfxresources") }
}

sourcesJar {
    mustRunAfter processResources
}

sass {
    // dart-sass version to use:
    version = "$sass"

    // Directory where to install dart-sass:
    directory = file("${rootDir}/.gradle/sass")

    // Base URL where to download dart-sass from:
    baseUrl = 'https://github.com/sass/dart-sass/releases/download'

    // Disable automatic copying to war (default is enabled):
    noAutoCopy()
}

compileSass {
    String baseDir = "io/github/palexdev/mfxresources/sass/themes"
    sourceDir = project.file("src/main/resources/" + baseDir)
    outputDir = project.file("src/main/resources/" + baseDir)
    sourceMap = none
}

processResources {
    // This makes gradle compile sass to css before copying the resources
    dependsOn compileSass
    dependsOn processCSS
}

mavenPublishing {
    publishToMavenCentral(SonatypeHost.S01)
    signAllPublications()
}