import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'biz.aQute.bnd.builder' version '5.2.0'
    id 'com.vanniktech.maven.publish' version '0.13.0'
}

repositories {
    mavenCentral()
    jcenter()
}

compileJava {
    sourceCompatibility = '11'
    targetCompatibility = '11'
}

dependencies {
    testImplementation('junit:junit:4.13')
    implementation 'com.vanniktech:gradle-maven-publish-plugin:0.13.0'

    implementation 'org.reactfx:reactfx:2.0-M5'
}

javadoc {
    excludes = ['**/*.html', 'META-INF/**']

    options.use = true
    options.splitIndex = true
    options.encoding = 'UTF-8'
    options.author = true
    options.version = true
    options.windowTitle = "$project.name $project.version API"
    options.docTitle = "$project.name $project.version API"
    options.links = ['https://docs.oracle.com/en/java/javase/11/docs/api',
                     'https://openjfx.io/javadoc/15']
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

task sourcesJarBuild(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJarBuild
    archives jar
}

jar {
    manifest {
        attributes(
                'Bundle-Name': project.name,
                'Bundle-Description': "Material controls for JavaFX",
                'Bundle-SymbolicName': 'io.github.palexdev',
                'Export-Package': 'io.github.palexdev.materialfx.*, io.github.palexdev.materialfx.demo.*'
        )
    }
}

task copyJar(type: Copy) {
    from jar
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        into System.getenv("APPDATA") + '/Scene Builder/Library'
    } else if(Os.isFamily(Os.FAMILY_MAC)) {
        into System.getProperty("user.home") + '/Library/Application Support' + '/Scene Builder/Library'
    } else if(Os.isFamily(Os.FAMILY_UNIX)) {
        into System.getProperty("user.home") + '/.scenebuilder/Library'
    }
}

task removeBnd(type: Delete) {
    delete fileTree(project.buildDir) {
        include '**/*.bnd'
    }
}

publishing {
    publications {
        main(MavenPublication) {
            pom.withXml {
                asNode().dependencies.'*'.findAll {
                    it.groupId.text() == 'org.openjfx'
                }.each {
                    it.parent().remove(it)
                }
            }
        }
    }
}

build.dependsOn copyJar, removeBnd
